Function:
  Enabled For RDMLX:  Yes

  Source: |
    Function Options(*DIRECT) Rcv_List(#wLKData #wLRet)
    
    * ******************************************************************************************************
    * Test Case Description
    * Issue #43 Test SELECT with *BACKWARDS
    *
    *
    * ******************************************************************************************************
    
    * Unique Key Data.
    Def_List Name(#wLKData) Fields(#VTF_KEY1 #VTF_KEY2 #VTF_KEY3) Type(*WORKING) Entrys(1)
    
    * Return messages.
    Def_List Name(#wLRet) Fields(#BIFMSGC #BIFMSG1 #BIFMSG2) Counter(#BIFMSGCNT) Type(*WORKING) Entrys(*MAX)
    
    Define Field(#LF_rc) Reffld(#BIFA002A)
    Define Field(#LF_DB_Type) Type(*CHAR) Length(32)
    Define Field(#LF_i) Reffld(#Std_Count)
    Define Field(#LF_j) Reffld(#Std_Count)
    Define Field(#LF_Original_Count) Reffld(#Std_Count)
    Define Field(#LF_DB_Count) Reffld(#Std_Count)
    Define Field(#LF_DataShown) Reffld(#BIFB001B)
    
    Define Field(#LF_Alphabet) Reffld(#BIFA009A) Default('ABCDE')
    Define Field(#LF_BIFN0700A_FromCount) Reffld(#std_count) Default(-4)
    Define Field(#LF_BIFN0700A_ToCount) Reffld(#std_count) Default(10)
    Define Field(#LF_BIFC010A_Count) Reffld(#std_count) Default(5)
    Define Field(#LF_WorkingCount) Reffld(#std_count)
    
    
    * Local Copies of Table Columns - matches following List fields
    Define Field(#LF_BIFN0700A) Reffld(#BIFN0700A)
    Define Field(#LF_BIFN0700A_key) Reffld(#BIFN0700A)
    Define Field(#LF_BIFC010A) Reffld(#BIFC010A)
    
    
    * #XG_ALL includes all Fields in the Table
    Group_By Name(#XG_ALL) Fields(#BIFN0700A #BIFC010A)
    
    Def_List Name(#w_lOriginal) Fields(#XG_ALL) Counter(#LF_Original_Count) Type(*WORKING) Entrys(*MAX)
    Def_List Name(#w_lDB) Fields(#XG_ALL) Counter(#LF_DB_Count) Type(*WORKING) Entrys(*MAX)
    
    Use Builtin(Get_Session_Value) With_Args(DBUT) To_Get(#LF_DB_Type #LF_rc)
    
    * Start testing.
    Clr_List Named(#wLRet)
    #Com_Owner.mthAdd_Msg( OK *Function ('Start - ' + *Function_Desc) )
    
    Get_Entry Number(1) From_List(#wLKData)
    
    *
    * Add your test case here
    * It should be a list of method calls so its easy to read whats being tested.
    #Com_Owner.mthClearData( #LF_rc )
    
    If (#LF_rc *EQ OK)
    #Com_Owner.mthInsert( #LF_rc )
    Endif
    
    If (#LF_rc *EQ OK)
    #Com_Owner.mthSelectAllChar( #LF_rc )
    Endif
    
    If (#LF_rc *EQ OK)
    #Com_Owner.mthSelectSubsetChar( #LF_rc )
    Endif
    
    #Com_Owner.mthAdd_Msg( #LF_rc *Function 'End' )
    Use Builtin(CLR_MESSAGES)
    Return
    
    * Add a message to the Return Message List.
    Mthroutine Name(mthAdd_Msg)
    Define_Map For(*INPUT) Class(#BIFMSGC) Name(#this_msg_code)
    Define_Map For(*INPUT) Class(#BIFMSG1) Name(#this_msg1)
    Define_Map For(*INPUT) Class(#BIFMSG2) Name(#this_msg2)
    
    #BIFMSGC := #this_msg_code
    #BIFMSG1 := #this_msg1
    #BIFMSG2 := #this_msg2
    Add_Entry To_List(#wLRet)
    Endroutine
    
    * Add a sub message to the Return Message List.
    Mthroutine Name(mthAdd_SubMsg)
    Define_Map For(*INPUT) Class(#BIFMSGC) Name(#this_msg_code)
    Define_Map For(*INPUT) Class(#BIFMSG1) Name(#this_msg1)
    Define_Map For(*INPUT) Class(#BIFMSG2) Name(#this_msg2)
    
    #BIFMSGC := #this_msg_code
    #BIFMSG1 := ' ' + #this_msg1
    #BIFMSG2 := #this_msg2
    Add_Entry To_List(#wLRet)
    Endroutine
    
    *
    * Log the runtime messages produced by BIFs.
    * N.B. Only expected to be called when there is an error.
    * If it was always called the log would be swamped with irrelevant messages
    *
    Mthroutine Name(mthIssue_Runtime_Message) Options(*RECEIVES_MESSAGES)
    Define_Map For(*INPUT) Class(#BIFA002A) Name(#this_rc)
    
    Define Field(#LF_msg_rc) Reffld(#BIFA002A) Default(OK)
    Define Field(#LF_msg_text) Reffld(#BIFMSG1)
    
    Dowhile Cond(#LF_msg_rc = OK)
    Use Builtin(GET_MESSAGE) To_Get(#LF_msg_rc #LF_msg_text)
    If Cond(#LF_msg_rc = OK)
    #Com_Owner.mthAdd_SubMsg( #this_rc (#LF_msg_text) ('') )
    Endif
    Endwhile
    Endroutine
    
    *
    *
    *
    Mthroutine Name(mthClearData)
    Define_Map For(*BOTH) Class(#BIFA002A) Name(#this_rc)
    
    #Com_Owner.mthAdd_SubMsg( OK 'Clear Data' '' )
    
    Clr_List Named(#w_lOriginal)
    Clr_List Named(#w_lDB)
    
    Select Fields(#XG_ALL) From_File(VTLI0043C) Io_Status(#this_rc) Io_Error(*NEXT) Val_Error(*NEXT)
    Add_Entry To_List(#w_lOriginal)
    Endselect
    
    If (#this_rc = EF)
    #this_rc := OK
    #Com_Owner.mthAdd_SubMsg( OK ('- ' + #LF_Original_Count.AsString + ' record(s) found') '' )
    
    If (#LF_Original_Count > 0)
    Begin_Loop Using(#LF_i) To(#LF_Original_Count)
    Get_Entry Number(#LF_i) From_List(#w_lOriginal)
    
    Delete From_File(VTLI0043C) With_Key(#BIFN0700A #BIFC010A) Io_Status(#this_rc) Io_Error(*NEXT) Val_Error(*NEXT)
    If (#this_rc <> OK)
    #Com_Owner.mthIssue_Runtime_Message( #this_rc )
    #Com_Owner.mthAdd_SubMsg( ER ('- Unexpected ' + #this_rc) ('Deleting on record ' + #BIFP0300A.AsString) )
    #this_rc := ER
    Leave
    Endif
    End_Loop
    
    If (#this_rc = OK)
    #Com_Owner.mthAdd_SubMsg( OK ('- Successful') '' )
    Endif
    Endif
    Else
    #Com_Owner.mthAdd_SubMsg( ER ('- Unexpected ' + #this_rc) '' )
    #this_rc := ER
    Endif
    
    Clr_List Named(#w_lOriginal)
    Endroutine
    
    *
    *
    *
    Mthroutine Name(mthInsert)
    Define_Map For(*BOTH) Class(#BIFA002A) Name(#this_rc)
    
    * Create Test Data list
    
    Begin_Loop Using(#LF_I) From(#LF_BIFN0700A_FromCount) To(#LF_BIFN0700A_ToCount)
    Begin_Loop Using(#LF_J) To(#LF_BIFC010A_COUNT)
    #BIFN0700A := #LF_i
    #BIFC010A := #LF_Alphabet.Substring( #LF_J 1 )
    Add_Entry To_List(#w_lOriginal)
    End_Loop
    End_Loop
    
    #Com_Owner.mthAdd_SubMsg( OK ('Insert: ' + #LF_Original_Count.AsString + ' record(s)') '' )
    
    Selectlist Named(#w_lOriginal)
    Insert Fields(*ALL) To_File(VTLI0043C) Io_Status(#this_rc) Io_Error(*NEXT) Val_Error(*NEXT)
    If (#this_rc <> OK)
    #Com_Owner.mthIssue_Runtime_Message( #this_rc )
    #Com_Owner.mthAdd_SubMsg( ER ('- Unexpected ' + #this_rc) ('Inserting record ' + #BIFP0300A.AsString + ', ' + #BIFA256A) )
    Leave
    Endif
    Endselect
    
    If (#this_rc = OK)
    #Com_Owner.mthAdd_SubMsg( OK ('- Successful') '' )
    Endif
    Endroutine
    
    *
    * mthSelectAllChar
    *
    Mthroutine Name(mthSelectAllChar)
    Define_Map For(*BOTH) Class(#BIFA002A) Name(#this_rc)
    
    #Com_Owner.mthAdd_SubMsg( OK ('Compare All Rows Backwards: ' + #LF_Original_Count.AsString + ' record(s)') '' )
    
    Clr_List Named(#w_lDB)
    #LF_BIFN0700A_key := 2
    Select Fields(*ALL) From_File(VTLI0043C) Io_Status(#this_rc) Io_Error(*NEXT) Val_Error(*NEXT) Options(*BACKWARDS)
    Add_Entry To_List(#w_lDB)
    Endselect
    
    If (#this_rc *EQ ER)
    #Com_Owner.mthIssue_Runtime_Message( #this_rc )
    #Com_Owner.mthAdd_SubMsg( ER ('- Unexpected ' + #this_rc) ('Selecting record ' + #LF_DB_Count.AsString) )
    Return
    Endif
    
    If (#LF_Original_Count *NE #LF_DB_Count)
    #Com_Owner.mthAdd_SubMsg( ER ('- Expected # rows ' + #LF_Original_Count.asString) ('Actual rows ' + #LF_DB_Count.AsString) )
    Return
    Endif
    
    * Loop through the original list backwards and the DB list forwards (which has the BACKWARDS rows)
    #LF_j := 1
    Begin_Loop Using(#LF_i) From((#LF_BIFN0700A_ToCount - #LF_BIFN0700A_FromCount + 1) * #LF_BIFC010A_COUNT) To(1) Step(-1)
    Get_Entry Number(#LF_i) From_List(#w_lOriginal) Ret_Status(#this_rc)
    #LF_BIFN0700A := #BIFN0700A
    #LF_BIFC010A := #BIFC010A
    
    Get_Entry Number(#LF_j) From_List(#w_lDB) Ret_Status(#this_rc)
    If (#this_rc <> OK)
    #Com_Owner.mthIssue_Runtime_Message( #this_rc )
    #Com_Owner.mthAdd_SubMsg( ER ('- Unexpected ' + #this_rc) ('Entry Number ' + #LF_j.AsString + ' may not exist in list LF_lDB') )
    Leave
    Endif
    
    #Com_Owner.mthCompareAllFields( #this_rc )
    If (#this_rc <> OK)
    Leave
    Endif
    
    #LF_j += 1
    End_Loop
    
    If (#this_rc = OK)
    #Com_Owner.mthAdd_SubMsg( OK ('- Successful') '' )
    Endif
    Endroutine
    
    *
    * mthSelectSubsetChar
    *
    Mthroutine Name(mthSelectSubsetChar)
    Define_Map For(*BOTH) Class(#BIFA002A) Name(#this_rc)
    
    #LF_BIFN0700A_key := 2
    
    Clr_List Named(#w_lDB)
    Select Fields(*ALL) From_File(VTLI0043C) With_Key(#LF_BIFN0700A_key) Io_Status(#this_rc) Io_Error(*NEXT) Val_Error(*NEXT) Options(*BACKWARDS)
    Add_Entry To_List(#w_lDB)
    Endselect
    
    #Com_Owner.mthAdd_SubMsg( OK ('Compare Rows Backwards #BIFN0700A = ' + #LF_BIFN0700A_key.AsString + ' : ' + #LF_DB_Count.AsString + ' record(s)') '' )
    
    If (#this_rc *EQ ER)
    #Com_Owner.mthIssue_Runtime_Message( #this_rc )
    #Com_Owner.mthAdd_SubMsg( ER ('- Unexpected ' + #this_rc) ('Selecting record ' + #LF_DB_Count.AsString) )
    Return
    Endif
    
    #LF_WorkingCount := #LF_Original_Count / (#LF_BIFN0700A_ToCount - #LF_BIFN0700A_FromCount)
    
    * Expecting 1 set of Numeric data (2,A), (2,B), etc
    If (#LF_WorkingCount *NE #LF_DB_Count)
    #Com_Owner.mthAdd_SubMsg( ER ('- Expected # rows ' + #LF_WorkingCount.asString) ('Actual rows ' + #LF_DB_Count.AsString) )
    Return
    Endif
    
    * Loop through the original list backwards and the DB list forwards (which has the BACKWARDS rows)
    #LF_j := 1
    Begin_Loop Using(#LF_i) From((#LF_BIFN0700A_ToCount - #LF_BIFN0700A_FromCount + 1) * #LF_BIFC010A_COUNT) To(1) Step(-1)
    Get_Entry Number(#LF_i) From_List(#w_lOriginal) Ret_Status(#this_rc)
    #LF_BIFN0700A := #BIFN0700A
    #LF_BIFC010A := #BIFC010A
    
    * Only check the matching rows
    If (#LF_BIFN0700A *NE #LF_BIFN0700A_key)
    Continue
    Endif
    
    Get_Entry Number(#LF_j) From_List(#w_lDB) Ret_Status(#this_rc)
    If (#this_rc <> OK)
    #Com_Owner.mthIssue_Runtime_Message( #this_rc )
    #Com_Owner.mthAdd_SubMsg( ER ('- Unexpected ' + #this_rc) ('Entry Number ' + #LF_j.AsString + ' may not exist in list LF_lDB') )
    Leave
    Endif
    
    #Com_Owner.mthCompareAllFields( #this_rc )
    If (#this_rc <> OK)
    Leave
    Endif
    
    #LF_j += 1
    End_Loop
    
    If (#this_rc = OK)
    #Com_Owner.mthAdd_SubMsg( OK ('- Successful') '' )
    Endif
    Endroutine
    
    Mthroutine Name(mthCompareAllFields)
    Define_Map For(*BOTH) Class(#BIFA002A) Name(#this_rc)
    * Compare all the fields in the 2 lists
    #LF_DataShown := False
    
    * Debug line - needs to be removed once the test is fully working
    * #Com_Owner.mthShowData( #this_rc )
    
    If (#LF_BIFN0700A <> #BIFN0700A)
    #Com_Owner.mthShowData( #this_rc )
    #Com_Owner.mthAdd_SubMsg( ER ('- Field #BIFN0700A data mismatch') '' )
    #this_rc := ER
    Endif
    If (#LF_BIFC010A <> #BIFC010A)
    #Com_Owner.mthShowData( #this_rc )
    #Com_Owner.mthAdd_SubMsg( ER ('- Field #BIFC010A data mismatch') '' )
    #this_rc := ER
    Endif
    
    
    If (#this_rc <> OK)
    #Com_Owner.mthAdd_SubMsg( ER ('- Unexpected ' + #this_rc) ('Comparing record ') )
    Return
    Endif
    Endroutine
    *
    * mthShowData
    *
    Mthroutine Name(mthShowData)
    Define_Map For(*BOTH) Class(#BIFA002A) Name(#this_rc)
    
    If (*Not #LF_DataShown)
    #LF_DataShown := True
    #Com_Owner.mthAdd_SubMsg( OK ('- data mismatch on Entry Number ' + #LF_i.AsString) ('') )
    
    #Com_Owner.mthAdd_SubMsg( OK ('- Real Fields') ('') )
    
    #Com_Owner.mthAdd_SubMsg( OK ('- Original #BIFN0700A "' + #LF_BIFN0700A.asString + '"') '' )
    #Com_Owner.mthAdd_SubMsg( OK ('- DB       #BIFN0700A "' + #BIFN0700A.asString + '"') '' )
    
    #Com_Owner.mthAdd_SubMsg( OK ('- Original #BIFC010A "' + #LF_BIFC010A + '"') '' )
    #Com_Owner.mthAdd_SubMsg( OK ('- DB       #BIFC010A "' + #BIFC010A + '"') '' )
    
    
    Endif
    
    Endroutine
    
    *
    *
    *
    Mthroutine Name(mthSigned2String)
    Define_Map For(*BOTH) Class(#BIFA002A) Name(#this_rc)
    Define_Map For(*INPUT) Class(#BIFN0702A) Name(#this_signed)
    Define_Map For(*RESULT) Class(#BIFA009A) Name(#this_string)
    
    Define Field(#LF_IntegerCount) Reffld(#BIFINT1A) Default(5)
    Define Field(#LF_DecimalCount) Reffld(#BIFINT1A) Default(2)
    Define Field(#LF_WorkString) Reffld(#BIFA256A)
    Define Field(#LF_IntegerPart) Reffld(#BIFA005A)
    Define Field(#LF_DecimalPart) Reffld(#BIFA002A)
    Define Field(#LF_DecimalPosn) Reffld(#BIFINT1A)
    #LF_WorkString := #BIFN0702A.AsString
    #LF_DecimalPosn := #LF_WorkString.PositionOf( '.' )
    If (#LF_DecimalPosn = 0)
    #LF_IntegerPart := #LF_WorkString.Substring( 1 #LF_IntegerCount ).RightAdjust( #LF_IntegerCount '0' )
    #LF_DecimalPart := *NULL
    #LF_DecimalPart := #LF_DecimalPart.RightTrim.LeftMost( #LF_DecimalCount '0' )
    Else
    #LF_IntegerPart := #LF_WorkString.Substring( 1 (#LF_DecimalPosn - 1) ).RightAdjust( #LF_IntegerCount '0' )
    #LF_DecimalPart := #LF_WorkString.Substring( (#LF_DecimalPosn + 1) #LF_DecimalCount ).RightTrim.LeftMost( #LF_DecimalCount '0' )
    Endif
    #this_string := #LF_IntegerPart + #LF_DecimalPart
    Endroutine
    
    Mthroutine Name(mthDummy)
    Define_Map For(*BOTH) Class(#BIFA002A) Name(#this_rc)
    Select Fields(*ALL) From_File(VTLI0043C) Io_Status(#this_rc) Io_Error(*NEXT) Val_Error(*NEXT) Options(*STARTKEY)
    Add_Entry To_List(#w_lDB)
    Endselect
    
    Select Fields(*ALL) From_File(VTLI0043C) Io_Status(#this_rc) Io_Error(*NEXT) Val_Error(*NEXT) Options(*ENDWHERE)
    Add_Entry To_List(#w_lDB)
    Endselect
    
    Select Fields(*ALL) From_File(VTLI0043C) Io_Status(#this_rc) Io_Error(*NEXT) Val_Error(*NEXT) Options(*ENDWHERESQL)
    Add_Entry To_List(#w_lDB)
    Endselect
    Endroutine
